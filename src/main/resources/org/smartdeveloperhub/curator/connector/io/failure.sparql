#
# #-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=#
#   This file is part of the Smart Developer Hub Project:
#     http://www.smartdeveloperhub.org/
#
#   Center for Open Middleware
#     http://www.centeropenmiddleware.com/
# #-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=#
#   Copyright (C) 2015-2016 Center for Open Middleware.
# #-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=#
#   Licensed under the Apache License, Version 2.0 (the "License");
#   you may not use this file except in compliance with the License.
#   You may obtain a copy of the License at
#
#             http://www.apache.org/licenses/LICENSE-2.0
#
#   Unless required by applicable law or agreed to in writing, software
#   distributed under the License is distributed on an "AS IS" BASIS,
#   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#   See the License for the specific language governing permissions and
#   limitations under the License.
# #-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=#
#   Artifact    : org.smartdeveloperhub.curator:sdh-curator-connector:0.2.0
#   Bundle      : sdh-curator-connector-0.2.0.jar
# #-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=#
#

PREFIX stoa:  <http://www.smartdeveloperhub.org/vocabulary/stoa#>
PREFIX types: <http://www.smartdeveloperhub.org/vocabulary/types#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>

SELECT
  ?failure
  ?messageId ?submittedOn ?submittedBy
  ?responseTo ?responseNumber
  ?code ?reason
  ?subcode ?detail
WHERE {
  ?failure
    a stoa:Failure ;
    stoa:messageId ?messageId ;
    stoa:submittedOn ?submittedOn ;
    stoa:submittedBy ?submittedBy ;
    stoa:responseTo ?responseTo ;
    stoa:responseNumber ?responseNumber ;
    stoa:code ?code ;
    stoa:reason ?reason .
  OPTIONAL { ?failure stoa:replyTo ?replyTo . }
  OPTIONAL { ?failure stoa:subcode ?subcode . }
  OPTIONAL { ?failure stoa:detail ?detail . }
  FILTER (
    (datatype(?messageId) = types:UUID) &&
    (datatype(?submittedOn) = xsd:dateTime) &&
    !isLiteral(?submittedBy) &&
    (datatype(?responseTo) = types:UUID) &&
    (datatype(?responseNumber) = xsd:unsignedLong) &&
    (datatype(?code) = xsd:unsignedLong) &&
    (datatype(?reason) = xsd:string) &&
    !(bound(?subcode) && (datatype(?subcode) != xsd:unsignedLong)) &&
    !(bound(?detail) && (datatype(?detail) != xsd:string)) &&
    !bound(?replyTo)
  )
}
