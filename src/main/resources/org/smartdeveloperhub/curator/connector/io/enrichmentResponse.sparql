#
# #-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=#
#   This file is part of the Smart Developer Hub Project:
#     http://www.smartdeveloperhub.org/
#
#   Center for Open Middleware
#     http://www.centeropenmiddleware.com/
# #-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=#
#   Copyright (C) 2015-2016 Center for Open Middleware.
# #-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=#
#   Licensed under the Apache License, Version 2.0 (the "License");
#   you may not use this file except in compliance with the License.
#   You may obtain a copy of the License at
#
#             http://www.apache.org/licenses/LICENSE-2.0
#
#   Unless required by applicable law or agreed to in writing, software
#   distributed under the License is distributed on an "AS IS" BASIS,
#   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#   See the License for the specific language governing permissions and
#   limitations under the License.
# #-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=#
#   Artifact    : org.smartdeveloperhub.curator:sdh-curator-connector:0.2.0-SNAPSHOT
#   Bundle      : sdh-curator-connector-0.2.0-SNAPSHOT.jar
# #-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=#
#

PREFIX curator: <http://www.smartdeveloperhub.org/vocabulary/curator#>
PREFIX types: <http://www.smartdeveloperhub.org/vocabulary/types#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT
  ?enrichmentResponse
  ?messageId ?submittedOn ?submittedBy
  ?responseTo ?responseNumber
  ?targetResource ?additionTarget ?removalTarget
WHERE {
  ?enrichmentResponse
    a curator:EnrichmentResponse ;
    curator:messageId ?messageId ;
    curator:submittedOn ?submittedOn ;
    curator:submittedBy ?submittedBy ;
    curator:responseTo ?responseTo ;
    curator:responseNumber ?responseNumber ;
    curator:targetResource ?targetResource .
  OPTIONAL {
    ?enrichmentResponse curator:additionTarget ?additionTarget .
    ?additionTarget a curator:Variable .
   }
  OPTIONAL {
    ?enrichmentResponse curator:removalTarget ?removalTarget .
    ?removalTarget a curator:Variable .
  }
  OPTIONAL { ?enrichmentResponse curator:replyTo ?replyTo . }
  FILTER (
    (datatype(?messageId) = types:UUID) &&
    (datatype(?submittedOn) = xsd:dateTime) &&
    !isLiteral(?submittedBy) &&
    (datatype(?responseTo) = types:UUID) &&
    (datatype(?responseNumber) = xsd:unsignedLong) &&
    isURI(?targetResource) &&
    !(bound(?additionTarget) && !isBlank(?additionTarget)) &&
    !(bound(?removalTarget) && !isBlank(?removalTarget)) &&
    !bound(?replyTo)
  )
}
